#!ipxe

# Fedora & CentOS

set ksurl http://pxe.salstar.sk/ks/

goto ${menu} ||

# OS
:fedora
set os Fedora
set devel_version 17
menu Fedora
item --key 6 16 ${os} 16 ${arch} [6]
item --key 5 15 ${os} 15 ${arch} [5]
item --key a Alpha ${os} ${devel_version}-Aplha ${arch} [a]
item --key b Beta ${os} ${devel_version}-Beta ${arch} [b]
item --key d development ${os} ${devel_version} (development) ${arch} [d]
item --key f fedoraproject ${os} boot.fedoraproject.org [f]
isset ${version} || choose version || goto main_menu
goto fedora_${version} ||
set dir fedora/linux/releases/${version}/${os}/${arch}/os
:fedora_setvars
set osks ${ksurl}auto
goto boottype

:fedora_development
set dir fedora/linux/development/${devel_version}/${arch}/os
set params root=live:${mirror}/${dir}/LiveOS/squashfs.img ${params}
goto fedora_setvars

:fedora_Alpha
set version ${devel_version}-Alpha
set dir fedora/linux/releases/test/${version}/${os}/${arch}/os
set params root=live:${mirror}/${dir}/LiveOS/squashfs.img ${params}
goto fedora_setvars

:fedora_Beta
set version ${devel_version}-Beta
set dir fedora/linux/releases/test/${version}/${os}/${arch}/os
set params root=live:${mirror}/${dir}/LiveOS/squashfs.img ${params}
goto fedora_setvars

:fedora_live
set dir fedora/linux/releases/${version}/Live/${arch}
set img ${os}-${version}-${arch}-Live-Desktop.iso
prompt
goto main_menu

# boot.fedoraproject.org online boot
:fedora_fedoraproject
echo ${cls}
imgverify boot.fedoraproject.org.ipxe ${sigs}boot.fedoraproject.org.ipxe.sig
chain boot.fedoraproject.org.ipxe
exit

:centos
set os CentOS
menu ${os}
item --key 6 6 ${os} 6 ${arch} [6]
item --key 5 5 ${os} 5 ${arch} [5]
isset ${version} || choose version || goto main_menu
set dir ${menu}/${version}/os/${arch}
set osks ${ksurl}centos${version}
goto boottype

:scientific
set os Scientific Linux
menu ${os}
item --key 6 6 ${os} 6 ${arch} [6]
item --key 5 5 ${os} 5 ${arch} [5]
isset ${version} || choose version || goto main_menu
set dir ${menu}/${version}x/${arch}/os
iseq ${version} 5 && set dir ${menu}/${version}x/${arch} ||
set osks ${ksurl}centos${version}
set mirror http://ftp.scientificlinux.org/linux
goto boottype

:boottype
set ova ${os} ${version} ${arch}
menu ${os} boot type
item normal ${ova}
item --key k ks ${ova} kickstart [k]
item --key r rescue ${ova} rescue [r]
item --key s remote ${ova} rescue with ssh [s]
item --key a autopart ${ova} kickstart part=root=4 [a]
item --key i isolinux ${ova} direct boot using isolinux [i]
isset ${testing} && item --key l live_desktop ${ova} Live Desktop [l] ||
isset ${testing} && item --key d live_kde ${ova} Live KDE [d] ||
isset ${bt} || choose bt || goto main_menu
echo ${cls}
iseq ${bt} ks && set params ksdevice=bootif ks=${osks} ${params} ||
iseq ${bt} rescue && set params rescue ${params} ||
iseq ${bt} remote && set params ksdevice=bootif ks=${ksurl}rescue ${params} ||
iseq ${bt} autopart && set params ksdevice=bootif ks=${osks} root=part=4 ${params} ||
goto bootos_${bt} ||
goto bootos_images

:bootos_images
echo Loading from ${mirror}/${dir}/isolinux/
echo   method=${mirror}/${dir}
echo   ${kickstart} ${rescue} ${params}
#set next-server 158.197.16.70
# load vmlinuz and initrd
kernel ${mirror}/${dir}/isolinux/vmlinuz
iseq ${version} development && imgtrust --allow ||
iseq ${version} development && goto bootos_images_skip_verify_vmlinuz ||
imgverify vmlinuz ${sigs}${dir}/isolinux/vmlinuz.sig
:bootos_images_skip_verify_vmlinuz
initrd ${mirror}/${dir}/isolinux/initrd.img
iseq ${version} development && goto bootos_images_skip_verify_initrd ||
imgverify initrd.img ${sigs}${dir}/isolinux/initrd.img.sig
:bootos_images_skip_verify_initrd
imgargs vmlinuz nodhcp method=${mirror}/${dir} ${params}
imgstat
isset ${debug} && prompt ||
boot
goto main_menu

:bootos_isolinux
echo Loading isolinux from ${mirror}/${dir}/isolinux/
set 209:string isolinux.cfg
set 210:string ${mirror}/${dir}/isolinux/
imgload ${url}pxelinux.0
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot
goto main_menu

:bootos_live_desktop
set img ${os}-${version}-${arch}-Live-Desktop.iso
goto bootos_live

:bootos_live_kde
set img ${os}-${version}-${arch}-Live-KDE.iso
goto bootos_live

:bootos_live
set dir fedora/linux/releases/${version}/Live/${arch}
set next-server 158.197.16.70
set params ip=${ip}:${next-server}:${gateway}:${netmask} BOOTIF=${mac}
echo params: ${params}
echo WARNING: This does not really works!
#kernel ${url}memdisk iso raw ${params}
#initrd ${mirror}/${dir}/${img}
sanboot --no-describe ${mirror}/${dir}/${img}
boot

:main_menu
exit 0
