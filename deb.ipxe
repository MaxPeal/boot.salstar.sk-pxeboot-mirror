#!ipxe

set country sk

goto ${menu} ||
menu Debian or Ubuntu ?
item --key d debian Debian [d]
item --key u ubuntu Ubuntu [u]
choose menu || goto deb_exit
goto ${menu}

:debian
iseq ${mirror} ${default_mirror} && set debmirror http://ftp.${country}.debian.org/debian/ || set debmirror ${mirror}

set os Debian
menu ${os} ${arch_a}
# https://wiki.debian.org/DebianReleases
item --key s jessie ${os} 8 jessie (stable) [s]
item --key o wheezy ${os} 7 wheezy (oldstable) [o]
#item --key q squeeze ${os} 6.0 squeeze (oldstable) [q]
item --key t stretch ${os} stretch (testing) [t]
item --key u sid ${os} sid (unstable) [u]
choose ver || goto deb_exit
set ver_alias ${ver}
iseq ${ver} wheezy && set ver_alias stable ||
iseq ${ver} squeeze && set ver_alias oldstable ||
goto deb_boot_type

:ubuntu
#set debmirror http://ftp.ubuntu.com/ubuntu/
iseq ${mirror} ${default_mirror} && set debmirror http://${country}.releases.ubuntu.com/ubuntu/ || set debmirror ${mirror}

set os Ubuntu
menu ${os} ${arch_a}
# https://wiki.ubuntu.com/Releases
item --gap Current (supported)
item --key y yakkety ${os} 16.10 Yakkety Yak  [y]
item --key w xenial ${os} 16.04 LTS Xenial Xerus [x]
item --key t trusty ${os} 14.04 LTS Trusty Tahr [t]
item --key p precise ${os} 12.04 LTS Precise Pangolin [p]
item --gap End of life (unsupported)
item wily ${os} 15.10 Wily Werewolf
item vivid ${os} 15.04 Vivid Vervet
item utopic ${os} 14.10 Utopic Unicorn
item saucy ${os} 13.10 Saucy Salamander
item raring ${os} 13.04 Raring Ringtail
item quantal ${os} 12.10 Quantal Quetzal
item oneiric ${os} 11.10 Oneiric Ocelot
item natty ${os} 11.04 Natty Narwhal
# lucid's pxelinux scripts are not compatible :-(
#item lucid ${os} 10.04.3 LTS Lucid Lynx
item --gap Other versions
item --key D devel Future version (development) [D]
item --key o other Choose other version [o]
choose ver || goto deb_exit

iseq ${ver} other || goto skip_read_ver
echo ${cls}
set ver
echo -n Enter version: ${} && read ver
:skip_read_ver

:deb_boot_type
set diru dists/${ver}-updates/main/installer-${arch_a}/current/images/netboot/
set dir dists/${ver}/main/installer-${arch_a}/current/images/netboot/

menu ${os} ${ver} boot parameters
item --key k install ${os} install [k]
item --key g gtk ${os} graphical install [g]
item --key c rescue ${os} rescue mode [c]
item --key a automated ${os} automated install [a]
item --key e expert ${os} expert install [e]
item --key l live ${os} live boot [l]
# desktop selection for Debian only
iseq ${menu} debian && item --key d desktop ${os} desktop selection [d] ||
item --key p pxe ${os} pxelinux boot [p]
choose --default ${type} type || goto deb_exit

echo ${cls}
goto deb_${type}

:deb_desktop
menu ${os} desktop selection
item default Default
item kde KDE
item lxde LXDE
item xfce XFCE
choose --default ${desktop} desktop || goto deb_boot_type
iseq ${desktop} default && clear desktop_param || set desktop_param desktop=${desktop}
goto deb_boot_type

:deb_rescue
set install_params rescue/enable=true
goto deb_boot

:deb_automated
set install_params auto=true priority=critical
goto deb_boot

:deb_expert
set install_params priority=low
goto deb_boot

:deb_gtk
set install_params video=vesa:ywrap,mtrr vga=788
iseq ${ver} precise && set installer gtk/ ||
goto deb_boot

:deb_install
:deb_boot
set diru ${diru}${installer}${menu}-installer/${arch_a}/
set dir ${dir}${installer}${menu}-installer/${arch_a}/
imgfree
imgtrust --allow
echo Boot parameters: ${install_params} ${desktop_param} -- quiet ${params}
# fallback to non-updates dir
kernel ${debmirror}${diru}linux ${install_params} ${desktop_param} -- quiet ${params} || kernel ${debmirror}${dir}linux ${install_params} ${desktop_param} -- quiet ${params}
initrd ${debmirror}${diru}initrd.gz || initrd ${debmirror}${dir}initrd.gz
boot

:deb_pxe
set 209:string pxelinux.cfg/default
set 210:string ${debmirror}${dir}

imgfree
kernel ${url}pxelinux.0 ${params}
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot

:deb_live
menu ${os} desktop selection
item --key g gnome-desktop GNOME [g]
item --key k kde-desktop KDE [k]
item --key l lxde-desktop LXDE [l]
item --key x xfce-desktop XFCE [x]
item --key r rescue Rescue [r]
item --key s standard Standard [s]
choose desktop || goto deb_exit

nslookup host cdimage.debian.org
set url http://${host}/debian-cd/current-live/${arch_a}/webboot
#iseq ${ver_alias} stable && set ver_number 7.6.0 ||
#iseq ${ver_alias} oldstable && set ver_number 6.0.10 ||
# only latest version available :-(
set ver_number 8.8.0
echo Check debian-live version on this URL:
echo ${}  ${url}
echo -n Enter version: ${}
read ver_number
#nslookup host live.debian.net
#set url http://${host}/cdimage/release/${ver_alias}/${arch_a}/web
set deblive ${url}/debian-live-${ver_number}-${arch_a}-${desktop}
imgfree
imgtrust --allow
kernel ${deblive}.vmlinuz boot=live components fetch=${deblive}.squashfs live-config ${params}
initrd ${deblive}.initrd.img
boot

:deb_exit
exit 0
