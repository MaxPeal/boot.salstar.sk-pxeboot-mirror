#!ipxe

:start
set url https://boot.salstar.sk/
set mirror http://ftp.upjs.sk${mirror_port}/pub
set sigs ${url}sigs/

set cls:hex 1b:5b:4a  # ANSI clear screen sequence - "^[[J"

set memtest_version 4.20

isset ${admin_ip} || set admin_ip 158.197.240.41
isset ${arch} || set arch x86_64
isset ${menu} && goto ${menu} ||

isset ${ip} || dhcp || echo DHCP failed

imgtrust

:main_menu
clear version
set space:hex 20:20
iseq ${arch} i386 && set arch5 i586 || set arch5 ${arch}
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
menu SAL's BOOT MENU
item --gap Default:
item --key l local ${space} Boot local hdd [l]
item --gap Operating systems:
# RPM based systems
item --key f fedora ${space} Fedora ${arch} [f]
item --key c centos ${space} CentOS ${arch} [c]
isset ${extended_menu} && item scientific ${space} Scientific Linux ${arch} ||
isset ${extended_menu} && item --key o opensuse ${space} openSUSE ${arch} [o] ||
isset ${extended_menu} && item mageia ${space} Mageia ${arch5} ||
isset ${extended_menu} && item mandriva ${space} Mandriva ${arch5} ||
# Deb based systems
item --key b debian ${space} Debian ${arch_a} [b]
item --key u ubuntu ${space} Ubuntu ${arch_a} [u]
# Others
isset ${extended_menu} && item archlinux ${space} ArchLinux ${arch_a} ||
isset ${extended_menu} && item slackware ${space} Slackware ${bits} bit ||
item --key p pmagic ${space} Parted Magic [p]
isset ${extended_menu} && item --key z clonezilla ${space} Clonezilla [z] ||
#isset ${extended_menu} && item rescatux ${space} RescaTux ||
# BSD systems
isset ${extended_menu} && item bsd ${space} BSD systems ||
# DOS
item --key d dos ${space} FreeDOS [d]
isset ${extended_menu} || item --key x extended_menu ${space} Other systems [x]
item --gap Options:
iseq ${arch} x86_64 && set bits 64 || set bits 32
item --key a changebits ${space} Architecture: ${arch} (${bits}bit) [a]
item --key 0x09 params_menu ${space} Kernel parameters: ${params}
item --gap Tools:
item --key h hdt ${space} Hardware detection tool [h]
item --key m memtest ${space} MemTest86+ ${memtest_version} [m]
item --key s shell ${space} iPXE shell [s]
isset ${menu} && set timeout 0 || set timeout 60000
choose --timeout ${timeout} --default ${menu} menu || goto local
echo ${cls}
goto ${menu} ||
imgverify ${menu}.ipxe ${sigs}${menu}.ipxe.sig || goto error
chain ${menu}.ipxe || goto error
goto main_menu

:error
echo Error occured, press any key to return to menu ...
prompt
goto main_menu

:local
echo Booting from local disks ...
exit 0

:reload
echo Reloading menu.ipxe ...
imgverify menu.ipxe ${sigs}menu.ipxe
chain menu.ipxe

:pxelinux
echo Loading pxelinux ...
set next-server 158.197.16.70
set 209:string pxelinux.cfg/default
set 210:string tftp://${next-server}/
kernel ${url}pxelinux.0
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot

:hdt
set 209:string hdt
set 210:string ${url}pxelinux.cfg/
kernel ${url}pxelinux.0
imgverify pxelinux.0 ${sigs}pxelinux.0.sig
boot
goto main_menu

:memtest
kernel ${url}images/memtest
imgverify memtest ${sigs}images/memtest.sig
boot 

:shell
echo Type "exit" to return to menu.
# temporarily disable imgtrust for shell
imgtrust --allow
set menu main_menu
shell
imgtrust
goto main_menu

:changebits
iseq ${arch} x86_64 && set arch i386 || set arch x86_64
goto main_menu

# this is not used yet
:select_arch
menu Select architecture
item --gap Basic:
item --key 3 i386 i386 (32 bit) [3]
item --key 6 x86_64 x86_64/amd64 (64 bit) [6]
item --gap Extended:
item i486 i486
item i586 i586
item i686 i686
choose --default ${march} march || goto main_menu
goto set_arch_${march}

:set_arch_i386
set arch i386
set arch_a i386
set xarch x86
goto main_menu

:set_arch_x86_64
set arch x86_64
set arch_a amd64
set xarch x64
goto main_menu

:set_arch_i486
set arch i386
set arch_a i386
set xarch x86
goto main_menu

:set_arch_i586
set arch i386
set arch_a i386
set xarch x86
goto main_menu

:set_arch_i686
set arch i386
set arch_a i386
set xarch x86
goto main_menu

:params_menu
menu Kernel parameters
item --gap Current: ${params}
item --gap
#item --key x exit Return to main menu [x]
item --key s set Set parameters [s]
item --key v vnc VNC installation [v]
item --key 0 ttyS0 Serial console ttyS0 [0]
item --key 1 ttyS1 Serial cosnole ttyS1 [1]
choose kp || goto main_menu
echo ${cls}
goto params_${kp}

# Kernel parameters
:params_set
echo -n Enter parameters: ${} && read params
goto params_menu

:params_vnc
set params ${params} vnc vncconnect=${admin_ip}:5500
goto params_menu

:params_ttyS0
set params ${params} console=ttyS0
goto params_menu

:params_ttyS1
set params ${params} console=ttyS1
goto params_menu

:extended_menu
isset ${extended_menu} && clear extended_menu || set extended_menu 1
goto main_menu

# OS
:fedora
:centos
:scientific
imgverify fedora.ipxe ${sigs}fedora.ipxe.sig
chain fedora.ipxe
goto main_menu

:debian
:ubuntu
imgverify deb.ipxe ${sigs}deb.ipxe.sig
chain deb.ipxe
goto main_menu
